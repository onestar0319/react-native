/*
 * Copyright (c) Meta Platforms, Inc. and affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */

plugins {
    id("com.android.library")
    id("maven-publish")
    id("signing")
    id("org.jetbrains.kotlin.android")
}

group = "com.facebook.react"
version = parent.publishing_version

repositories {
    // Normally RNGP will set repositories for all modules,
    // but when consumed from source, we need to re-declare
    // those repositories as there is no app module there.
    mavenCentral()
    google()
}

android {
    compileSdk 33
    buildToolsVersion = "33.0.1"
    namespace "com.facebook.react.flipper"

    defaultConfig {
        minSdk = 21
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlin {
        jvmToolchain(11)
    }

    dependencies {
        implementation project(':packages:react-native:ReactAndroid')
        debugImplementation("com.facebook.flipper:flipper:${FLIPPER_VERSION}")
        debugImplementation("com.facebook.flipper:flipper-network-plugin:${FLIPPER_VERSION}") {
            exclude group:'com.squareup.okhttp3', module:'okhttp'
        }
        debugImplementation("com.facebook.fresco:flipper-fresco-plugin:${FLIPPER_FRESCO_VERSION}")
    }

    publishing {
        multipleVariants {
            withSourcesJar()
            allVariants()
        }
    }
}

apply from: "../publish.gradle"
